(define (domain rearrangement-notyping)
  (:requirements :strips)
  (:predicates
     (IsRobot ?robot)
     (IsPawn ?pawn)
     (IsBear ?bear)
     (IsGoal ?goal)
     (IsMonkey ?monkey)
     (At ?obj ?loc)
     (Holding ?obj)
     (Handsfree ?robot)
     (MoveTo ?loc)
     (Pick ?obj)
     (Place ?obj)
  )

  ; (:actions MoveTo Pick Place)

  (:action MoveToNotHolding
    :parameters (?robot ?start ?end)
    :precondition (and (MoveTo ?end)
                       (IsRobot ?robot)
                       (Handsfree ?robot)
                       (At ?robot ?start)
                  )
    :effect (and (not (At ?robot ?start))
                 (At ?robot ?end)
            )
  )

  (:action MoveToHolding
    :parameters (?robot ?obj ?start ?end)
    :precondition (and (MoveTo ?end)
                       (IsRobot ?robot)
                       (Holding ?obj)
                       (At ?robot ?start)
                       (At ?obj ?start)
                  )
    :effect (and (not (At ?robot ?start))
                 (At ?robot ?end)
                 (not (At ?obj ?start))
                 (At ?obj ?end)
            )
  )

  (:action Pick
    :parameters (?robot ?obj ?loc)
    :precondition (and (Pick ?obj)
                       (IsRobot ?robot)
                       (Handsfree ?robot)
                       (At ?robot ?loc)
                       (At ?obj ?loc)
                       (not (IsRobot ?obj))
                  )
    :effect (and (Holding ?obj)
                 (not (Handsfree ?robot))
            )
  )

  (:action Place
    :parameters (?robot ?obj)
    :precondition (and (Place ?obj)
                       (IsRobot ?robot)
                       (Holding ?obj)
                  )
    :effect (and (not (Holding ?obj))
                 (Handsfree ?robot)
            )
  )
)